***********************************************************************
* WRITTEN BY K.M. (SAM) BASS (SBASS,SJB,KBASS,KMB)                    *
***********************************************************************
*--------------------------------------------------------------------*
*                                                                    *
*              PROGRAM TO LIST LPALIST DATASET AND ITS VOLSER        *
*                                                                    *
*--------------------------------------------------------------------*
LPALIST  CSECT
LPALIST  AMODE 31
LPALIST  RMODE 24
         SAVE  (14,12),T,'LPALIST_&SYSDATC._&SYSTIME.'
         LR    R12,R15
         USING LPALIST,R12
         LA    R0,SAVE             @ OF NEW SAVE
         ST    R13,SAVE+4           SET PREV SAVE
         ST    R0,8(,R13)           SET NEXT SAVE
         LR    R13,R0
*--------------------------------------------------------------------*
*              GET CVT                                               *
*--------------------------------------------------------------------*
         L     R2,X'10'            CVT
         USING CVT,R2
         L     R2,CVTSMEXT
         DROP  R2
         USING CVTVSTGX,R2
         L     R3,CVTEPLPS         LPALIST TABLE POINTER
         DROP  R2
         TPUT  =CL60'*** LIST OF LPA DATASETS ***',60
         CLC   =C'LPAT',0(R3)      IS IT REALLY LPA TABLE
         BE    LLTOK
         TPUT  =CL60'*** LPA LIST TABLE INVALID ***',60
         B     RETURN
LLTOK    DS    0H
         GETMAIN R,LV=LWORK,SP=1
         LR    R10,R1
         USING WORK,R10
         LR    R15,R0
         LR    R14,R1
         SLR   R1,R1
         MVCL  R14,R0              ZERO GETMAIN
         SPACE 1
         ZAP   DSNNUM,=P'0'        ZERO COUNTER
         LA    R3,4(,R3)           BYPASS 'LPAT'
         L     R4,0(,R3)           GET NUMBER OF ENTRIES
         LA    R5,4(,R3)           POINT AT ENTRIES
*--------------------------------------------------------------------*
*              LOOP THRU LLT TO GET EACH DATASET                     *
*--------------------------------------------------------------------*
LOOP     DS    0H
         SLR   R2,R2
         IC    R2,0(,R5)           ENTRY LENGTH
         MVC   REC,BLANKS
         S     R2,=F'1'
         MVC   REC+$DSN(0),1(R5)   MOVE IN DSN
         EX    R2,*-6               FOR REAL
*--------------------------------------------------------------------*
*              ISSUE LOCATE TO FIND VOLSER OF DATASET                *
*--------------------------------------------------------------------*
         MVI   CAMWORK2,X'FF'
         MVC   CAMWORK2+1(255),CAMWORK2
         XC    CAMLIST,CAMLIST     ZERO CAMLIST
         MVC   CAMLIST(4),=XL4'44000000' LOCATE PARMS
         LA    R0,REC+$DSN
         ST    R0,CAMLIST+4        @ OF DATASET
         LA    R0,CAMWORK
         ST    R0,CAMLIST+12       @ OF CAMLIST WORK AREA
         SPACE 1
         LA    R1,CAMLIST
LOCATE   SVC   26                  LOCATE DATASET
         SPACE 1
         MVC   REC+$OFF(4),=X'F0212020'
         ED    REC+$OFF(4),DSNNUM
         MVI   REC+$OFF,C'+'
         MVC   REC+$VOL(6),CAMWORK+6
         TPUT  REC,L'REC
         LA    R5,1+44(,R5)        POINT AT NEXT ENTRY
         AP    DSNNUM,=P'1'
         BCT   R4,LOOP
RETURN   DS    0H
         FREEMAIN R,LV=LWORK,A=(10),SP=1
         L     R13,4(,R13)
         RETURN (14,12),RC=0
*--------------------------------------------------------------------*
*              DATA AREA                                             *
*--------------------------------------------------------------------*
         LTORG ,
$OFF     EQU   1
$VOL     EQU   6
$DSN     EQU   $VOL+7
BLANKS   DC    CL80' '
SAVE     DC    9D'0'
*--------------------------------------------------------------------*
*              DSECTS                                                *
*--------------------------------------------------------------------*
WORK     DSECT
CAMLIST  DC    XL256'0'
         DS    0D                  ALIGNMENT
CAMWORK  DC    XL256'00'
CAMWORK2 DC    XL256'00'
         DS    0D
REC      DC    CL80' '
DSNNUM   DC    PL2'0'
         DS    0D                  ALIGNMENT
LWORK    EQU   *-WORK
         PRINT NOGEN
         YREGS
         CVT   DSECT=YES
         END   ,
