***********************************************************************
*        STANDALONE PROGRAM TO ISSUE A WTO TO CONSOLE                 *
*                                                                     *
*        //WTO   EXEC PGM=DSWTOPGM,PARM='XXXXXX YOUR WTO MSG'         *
*                                                                     *
***********************************************************************
*        ATTRIBUTES:                                                  *
*              REENTRANT,REUSABLE                                     *
*              AMODE=31,RMODE=24                                      *
***********************************************************************
*        CHANGE LOG:                                                  *
*        16AUG93 KBASS - RMODE=24 AND SET AMODE=31 IN PROGRAM         *
***********************************************************************
DSWTOMSG CSECT
DSWTOMSG RMODE 24                                               16AUG93
DSWTOMSG AMODE 31
         SAVE  (14,12),T,DSWTOMSG_&SYSDATC._&SYSTIME
         LR    R12,R15
         USING DSWTOMSG,R12         ADDRESS OUR BASE
         LR    R11,R1              SAVE PARM ADDRESS
         GETMAIN RU,LOC=BELOW,LV=WORKLEN,SP=1
*
         ST    R1,8(,R13)          CHAIN
         ST    R13,4(,R1)           SAVE AREAS
         LR    R13,R1                TOGETHER
         USING WORK,R13             ADDRESS OUR BASE
         L     R1,=A(X'80000000'+AM31)                          16AUG93
         BSM   R0,R1                                            16AUG93
AM31     DS    0H
         L     R6,0(,R11)           GET ADDRESS OF STORAGE AREA
         LH    R7,0(,R6)
         CH    R7,=Y(WTOL-8)
         BL    LENOK
         LH    R7,=Y(WTOL-8)
LENOK    DS    0H
         SH    R7,=H'1'
         BNP   RETURN
         MVC   XWTO,WTO
MVMSG    MVC   XWTO+4(*-*),2(R6)
         EX    R7,MVMSG
         LA    R7,1(,R7)           GET BACK REAL LENGTH
*--------------------------------------------------------------------*
*              SET WTO UP TO BE EXACT LENGTH OF PARM                 *
*--------------------------------------------------------------------*
         LA    R1,XWTO+4(R7)       POINT AT END+1 OF REAL DATA
         MVC   0(4,R1),XWTO+WTOL-4 MOVE IN
         LA    R7,4(,R7)           INCLUDE HEADER LENGTH
         STH   R7,XWTO             SAVE IN WTO
         WTO   ,MF=(E,XWTO)
RETURN   DS    0H                   RESTORE LINK REG
         LR    R1,R13
         L     R13,4(,R13)
         FREEMAIN RU,LV=WORKLEN,A=(1),SP=1
         RETURN (14,12),T,RC=0      RETURN TO CALLER
         ORG   *-2                 BACK UP OVER BR  R14         16AUG93
         BSM   R0,R14              RETURN IN CALLERS AMODE      16AUG93
*--------------------------------------------------------------------*
*              DATA AREA                                             *
*--------------------------------------------------------------------*
*                       1         2         3         4         5
*              12345678901234567890123456789012345678901234567890123456
WTO      WTO  '                                                        X
                                                           ',          X
               ROUTCDE=11,MF=L
WTOL     EQU   *-WTO
*--------------------------------------------------------------------*
*              WORK AREA MAPPING                                     *
*--------------------------------------------------------------------*
WORK     DSECT
SAVEAREA DS    18F
XWTO     DS    CL(WTOL)
WORKLEN  EQU   *-WORK
*--------------------------------------------------------------------*
*              EQUATES                                               *
*--------------------------------------------------------------------*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   DSWTOMSG
