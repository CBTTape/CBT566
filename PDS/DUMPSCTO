&GEN     SETC  'GEN'
***********************************************************************
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 3
*--------------------------------------------------------------------*
*              REGISTER AND OTHER EQUATES                            *
*--------------------------------------------------------------------*
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 2
         SPACE 3
         PRINT ON
*--------------------------------------------------------------------*
*              $$SUB MACRO                                           *
*--------------------------------------------------------------------*
         PRINT OFF
         MACRO
&L       $$SUB
&L       STM   R14,R12,12(R13)     SAVE REGS
         LA    R14,72(,R13)        NEXT SAVE
         ST    R14,8(,R13)         CHAIN
         ST    R13,4(,R14)          SAVE
         LR    R13,R14             NEW SAVE
         MEND
         PRINT ON
         EJECT
DUMPSCT  TITLE 'DUMPSCT - '
DUMPSCT  CSECT
         SAVE  (14,12),T,'DUMPSCT_&SYSDATC._&SYSTIME.'
         LR    R12,R15            POINT BASE REGISTER TO ENTRY POINT
         USING DUMPSCT,R12
         LA    R2,SAVEAREA        POINT TO SAVEAREA
         ST    R13,SAVEAREA+4     POINTER TO CALLERS SAVEAREA
         ST    R2,8(R13)          POINTER TO CALLED SAVEAREA
         LR    R13,R2             STANDARD POINTER TO SAVEAREA
*--------------------------------------------------------------------*
*              GET JCT ADDRESS                                       *
*--------------------------------------------------------------------*
         L     R2,PSATOLD-PSA(0)   GET TCBOLD ADDR
         L     R3,TCBJSCB-TCB(,R2) GET JSCB ADDRESS
         L     R3,JSCBACT-JSCB(,R3) GET JSCBACT ADDRESS
         L     R1,JSCBJCT-JSCB(,R3) GET JCT     ADDRESS
         SPACE 1
         L     R15,=V(SWAREQ)      *--------------------------- *
         BALR  R14,R15             * R1 = SVA   00FFFFFF FMT    *
         LTR   R15,R15             * CALL SWAREQ TO CONVERT SVA *
         BNZ   SWAERROR            * R1 = CONVERTED SVA, NOPREF *
         LR    R4,R1               *--------------------------- *
         SPACE 1
*--------------------------------------------------------------------*
*              GET SCT ADDRESS                                       *
*--------------------------------------------------------------------*
*        LA    R4,16(,R4)          BUMP PASS PREFIX
         L     R1,JCTSDKAD-JCT(,R4) GET SCT SVA ADDRESS
         SRL   R1,8                PUT IN LOW BYTES
         SPACE 1
         L     R15,=V(SWAREQ)      *--------------------------- *
         BALR  R14,R15             * R1 = SVA   00FFFFFF FMT    *
         LTR   R15,R15             * CALL SWAREQ TO CONVERT SVA *
         BNZ   SWAERROR            * R1 = CONVERTED SVA, NOPREF *
         LR    R4,R1               *--------------------------- *
         SPACE 1
SCTLOOP  DS    0H
         SL    R4,=F'16'           BACK UP TO PREFIX
         MVI   MSG1+19,C' '
         MVI   MSG2+19,C' '
         LA    R1,MSG0
         BAL   R14,PRTDMSG
         LA    R0,16
         LR    R1,R4               PRINT SCT PREFIX
         BAL   R14,PRTDUMP
         SPACE 1
         LA    R4,16(,R4)          POINT PASSED PREFIX
         LA    R0,SCTLNGTH         PICK UP ENTRY LENGTH
         CVD   R0,DEC
         MVC   DEC+08(6),=X'402020202120'
         ED    DEC+08(6),DEC+5
         MVC   MSG1+29(4),DEC+08+2
         ST    R0,DEC
         UNPK  DEC+8(05),DEC+2(3)
         MVC   MSG1+34(3),DEC+8+1
         TR    MSG1+34(3),TRANS-240
         LA    R1,MSG1
         BAL   R14,PRTDMSG
         LR    R1,R4
         BAL   R14,PRTDUMP
         LA    R0,SCTLNGTH         PICK UP ENTRY LENGTH
         LA    R1,MSG2
         BAL   R14,PRTDMSG
*--------------------------------------------------------------------*
*              GET SCTX ADDRESS                                      *
*--------------------------------------------------------------------*
         L     R1,SCTXBTTR-SCT(,R4) GET SCTX
         SRL   R1,8                SHIFT DOWN
         L     R15,=V(SWAREQ)      *--------------------------- *
         BALR  R14,R15             * R1 = SVA   00FFFFFF FMT    *
         LTR   R15,R15             * CALL SWAREQ TO CONVERT SVA *
         BNZ   SWAERROR            * R1 = CONVERTED SVA, NOPREF *
         LR    R5,R1               *--------------------------- *
         SL    R5,=F'16'           BACK UP TO PREFIX
         SPACE 1
         LA    R1,MSG0
         MVI   MSG0+19,C'X'
         BAL   R14,PRTDMSG
         LA    R0,16
         LR    R1,R5               PRINT JCTX PREFIX
         BAL   R14,PRTDUMP
         SPACE 1
         LA    R5,16(,R5)          POINT PASSED PREFIX
         LA    R0,SCTXLNTH         PICK UP ENTRY LENGTH
         CVD   R0,DEC
         MVC   DEC+08(6),=X'402020202120'
         ED    DEC+08(6),DEC+5
         MVC   MSG1+29(4),DEC+08+2
         MVI   MSG1+19,C'X'
         MVI   MSG2+19,C'X'
         ST    R0,DEC
         UNPK  DEC+8(05),DEC+2(3)
         MVC   MSG1+34(3),DEC+8+1
         TR    MSG1+34(3),TRANS-240
         LA    R1,MSG1
         BAL   R14,PRTDMSG
         SPACE 1
         LR    R1,R5
         BAL   R14,PRTDUMP
         LA    R0,SCTXLNTH         PICK UP ENTRY LENGTH
         LA    R1,MSG2
         BAL   R14,PRTDMSG
         SPACE 3
         L     R1,SCTANSCT-SCT(,R4) NEXT SCT ENTRY
         SRL   R1,8                SHIFT DOWN
         LTR   R1,R1
         BZ    DUMPRC00
         SPACE 1
         L     R15,=V(SWAREQ)      *--------------------------- *
         BALR  R14,R15             * R1 = SVA   00FFFFFF FMT    *
         LTR   R15,R15             * CALL SWAREQ TO CONVERT SVA *
         BNZ   SWAERROR            * R1 = CONVERTED SVA, NOPREF *
         LTR   R4,R1               *--------------------------- *
         B     SCTLOOP
*
DUMPRC00 DS    0H
         L     R13,4(,R13)
         RETURN (14,12),RC=0
*--------------------------------------------------------------------*
*              ERRORS                                                *
*--------------------------------------------------------------------*
SWAERROR DS    0H
         WTO   '*** SWAREQ ERROR ***',MF=L,ROUTCDE=11
         B     DUMPRC00
*--------------------------------------------------------------------*
*              WTOS                                                  *
*--------------------------------------------------------------------*
MSG0     WTO   '*** DUMP OF SCT  PREFIX',MF=L,                         X
               ROUTCDE=11
MSG1     WTO   '*** DUMP OF SCT - LENGTH=XXXX/XXX ***',MF=L,           X
               ROUTCDE=11
MSG2     WTO   '*** END  OF SCT  ***',MF=L,ROUTCDE=11
SAVEAREA DS    (4*9)D'0'
DEC      DS    2D
TRANS    DC    C'0123456789ABCDEF'
         SPACE 2
         COPY  PRTDUMP
         PRINT GEN
         MVSAREAS JSCB=YES,                                            X
               TCB=YES,                                                X
               JCT=YES,                                                X
               SCT=YES,                                                X
               PSA=YES,                                                X
               LISTGEN=YES
         END
