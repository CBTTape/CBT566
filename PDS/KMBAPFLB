         MACRO
&L       AUTH  &ONOFF
         AIF   ('&ONOFF' NE 'ON').OFF
         LA    R1,=X'3E1B1C37'     'AUTH' XC WITH FFFFFFFF
         XC    0(4,R1),=X'FFFFFFFF'
         SVC   245
         XC    0(4,R1),=X'FFFFFFFF'
         MEXIT
.OFF     ANOP
         SLR   R1,R1
         SVC   245
         MEND
*--------------------------------------------------------------------*
*                                                                    *
*              THIS PROGRAM WILL ADD A DATASET TO THE APF LIST       *
*              THAT IS POINTED TO BY THE APFLIB DDNAME               *
*                                                                    *
*    //KMBAPFAU JOB (1),KBASS,NOTIFY=&SYSUID,REGION=4M,              *
*    //         CLASS=X,MSGCLASS=X,MSGLEVEL=(1,1)                    *
*    /*JOBPARM  T=99,L=999                                           *
*    //*------- ----------------------------------------             *
*    //SB1      EXEC PGM=KMBAPFLB                                    *
*    //STEPLIB  DD  DISP=SHR,DSN=KBASS.PERM.LOAD                     *
*    //APFLIB   DD  DISP=SHR,DSN=SYS2.BBDB2.BBLINK                   *
*                                                                    *
*--------------------------------------------------------------------*
*        ADDED DYNAMIC APF SUPPORT                                   *
*                                                                    *
* KBASS1 04/04/2011 ADDED 'REMOVE' PARAMETER TO REMOVE AN APF LIBRARY*
*    //SB1      EXEC PGM=KMBAPFLB,PARM='REMOVE'                      *
*--------------------------------------------------------------------*
KMBAPFLB AMODE 31
KMBAPFLB RMODE 24
KMBAPFLB CSECT
         USING KMBAPFLB,R12
         SAVE  (14,12),T,KMBAPFLB_&SYSDATC._&SYSTIME
         LR    R12,R15
         LR    R11,R1              SAVE PARM ADDRESS
         L     R2,=A(SAVEAREA)
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
         USING DATAAREA,R10
         L     R10,=A(DATAAREA)
         AUTH  ON
         L     R15,=A(OPEN)        OPEN APFLIB
         BALR  R14,R15
         LTR   R15,R15             OK
         BNZ   ERROPEN              NO, EXIT
*--------------------------------------------------------------------*
*              CHECK PARM TO SEE IF IT IS 'REMOVE'                   *
*--------------------------------------------------------------------*
         SLR   R15,R15
         L     R1,0(,R11)
         ICM   R15,3,0(R1)         GET HALF WORK LENGTH
         BZ    NOTREMOV            NO PARM
         CH    R15,=H'6'           IS LENGTH CORRECT
         BNE   NOTREMOV            NO
         CLC   =C'REMOVE',2(R1)    IS IT 'REMOVE'
         BNE   NOTREMOV            NO
         OI    WFLAG,$REMOVE
NOTREMOV DS    0H
         SPACE 2
*--------------------------------------------------------------------*
*              FIND APF TABLE, IF DYNAMIC THEN $DYN IS SET (AND EXIT)*
*--------------------------------------------------------------------*
         L     R15,=A(GETAPFAD)
         BALR  R14,R15                     STD LINKAGE
         LTR   R15,R15
         BNZ   BADTABLE
         SPACE 2
         TM    WFLAG,$DYN          DID WE FIND APF IS DYANMIC TABLE
         BO    DYNAMIC              YES,
         SPACE 2
*--------------------------------------------------------------------*
*              THIS IS THE NON-DYNAMIC APF CODE                      *
*--------------------------------------------------------------------*
APFOK    L     R15,=A(SEARCH)      IS IT ALREADY THERE
         BALR  R14,R15
         CH    R15,H4               ALREADY THERE
         BE    DSNNOK
         CH    R15,H8               APF TABLE FULL
         BE    APFFULL
         BH    BADTABLE             BAD TABLE
         SPACE 2
         L     R15,=A(GETMAIN)     GET NEW STORAGE
         BALR  R14,R15
         LTR   R15,R15
         BNZ   GETMFAIL
         SPACE 2
         L     R15,=A(ASKIFOK)     ASK IT ITS OK TO ADD
         BALR  R14,R15
         LTR   R15,R15             OK TO ADD
         BNZ   DENIED               NO, EXIT
         SPACE 2
         L     R15,=A(BUILDNEW)
         BALR  R14,R15                     STD LINKAGE
         SPACE 2
         L     R15,=A(STIMER)      STIMER
         BALR  R14,R15
         SPACE 2
         L     R15,=A(FREEMAIN)    FREEMAIN OLD ENTRY
         BALR  R14,R15
         LTR   R15,R15
         BNZ   MAINRCXX
         SPACE 2
APFADDED DS    0H
         MVC   APFL001D,APFDSN
         XR    R0,R0               THIS MUST BE HERE
         WTO   ('APFL001 THE FOLLOWING DATASET SUCCESSFULLY ADDED',D), X
               ('APFL001 TO SYSTEM APFTABLE',D),                       X
               ('APFL001 DSN:                                          X
                 ',DE),                                                X
               ROUTCDE=(11)
APFL001D EQU   *-2-44,44
         XR    R15,R15
         B     MAINRCXX
*--------------------------------------------------------------------*
*              APF DATASET REMOVED                              040411
*--------------------------------------------------------------------*
APFREMOV DS    0H                                               040411
         MVC   APFL002D,APFDSN                                  040411
         XR    R0,R0               THIS MUST BE HERE            040411
         WTO   ('APFL002 THE FOLLOWING DSN SUCCESSFULLY REMOVED',D),   X
               ('APFL002 FROM SYSTEM APFTABLE',D),                     X
               ('APFL002 DSN:                                          X
                 ',DE),                                                X
               ROUTCDE=(11)                                     040411
APFL002D EQU   *-2-44,44                                        040411
         XR    R15,R15                                          040411
         B     MAINRCXX                                         040411
         EJECT ,
*--------------------------------------------------------------------*
*              APFLIB OPEN FAILED                                    *
*--------------------------------------------------------------------*
ERROPEN  DS    0H
         XR    R0,R0               THIS MUST BE HERE
         WTO   ('APFL230 APFLIB DD MISSING OR INCORRECT, CORRECT ',D), X
               ('APFL230 AND RESUBMIT JOB',DE),                        X
               ROUTCDE=(11)
         LA    R15,16
         B     MAINRCXX
         SPACE 2
*--------------------------------------------------------------------*
*              MESSED UP APFTABLE                                    *
*--------------------------------------------------------------------*
BADTABLE DS    0H
         MVC   APFL240D,APFDSN
         XR    R0,R0               THIS MUST BE HERE
         WTO   ('APFL240 THE FOLLOWING DATA SET NOT ADDED TO',D),      X
               ('APFL240 APFTABLE DUE TO UNKNOWN TABLE FORMAT',D),     X
               ('APFL240 DSN:                                          X
                 ',DE),                                                X
               ROUTCDE=(11)
APFL240D EQU   *-2-44,44
         LA    R15,20
         B     MAINRCXX
         SPACE 2
*--------------------------------------------------------------------*
*              DSN ALREADY IN APFTABLE                               *
*--------------------------------------------------------------------*
DSNNOK   DS    0H
         MVC   APFL250D,APFDSN
         XR    R0,R0               THIS MUST BE HERE
         WTO   ('APFL250 THE FOLLOWING DATASET IS ALREADY',D),         X
               ('APFL250 IN APFTABLE. REQUEST REJECTED',D),            X
               ('APFL250 DSN:                                          X
                 ',DE),                                                X
               ROUTCDE=(11)
APFL250D EQU   *-2-44,44
         LA    R15,24
         B     MAINRCXX
*--------------------------------------------------------------------*
*              GETMAIN FAILURE                                       *
*--------------------------------------------------------------------*
GETMFAIL MVC   APFL260D,APFDSN
         XR    R0,R0               THIS MUST BE HERE
         WTO   ('APFL260 THE FOLLOWING DATASET NOT ADDED TO ',D),      X
               ('APFL260 APFTABLE DUE TO INSUFFICENT SPACE',D),        X
               ('APFL260 DSN:                                          X
                 ',DE),                                                X
               ROUTCDE=(11)
APFL260D EQU   *-2-44,44
         LA    R15,12
         B     MAINRCXX
         SPACE 2
*--------------------------------------------------------------------*
*              APFTABLE IS FULL                                      *
*--------------------------------------------------------------------*
APFFULL  DS    0H
         MVC   APFL270D,APFDSN
         XR    R0,R0               THIS MUST BE HERE
         WTO   ('APFL270 THE FOLLOWING DATASET NOT ADDED TO ',D),      X
               ('APFL270 APFTABLE DUE TO APF TABLE ALREADY FULL',D),   X
               ('APFL270 DSN:                                          X
                 ',DE),                                                X
               ROUTCDE=(11)
APFL270D EQU   *-2-44,44
         LA    R15,24
         B     MAINRCXX
         SPACE 2
*--------------------------------------------------------------------*
*              OPERATOR DENIED REQUEST                               *
*--------------------------------------------------------------------*
DENIED   MVC   APFL280D,APFDSN
         XR    R0,R0               THIS MUST BE HERE
         WTO   ('APFL280 THE FOLLOWING DATASET NOT ADDED TO ',D),      X
               ('APFL280 APFTABLE DUE TO OPERATOR DENIAL',D),          X
               ('APFL280 DSN:                                          X
                 ',DE),                                                X
               ROUTCDE=(11)
APFL280D EQU   *-2-44,44
         LA    R15,8
         B     MAINRCXX
*--------------------------------------------------------------------*
*              DSN TO REMOVE NOT IN TABLE                            *
*--------------------------------------------------------------------*
REMDSNER DS    0H
         MVC   APFL290D,APFDSN
         XR    R0,R0               THIS MUST BE HERE
         WTO   ('APFL290 THE DATASET TO REMOVE FROM APFTABLE',D),      X
               ('APFL290 NOT PRESENT. REQUEST REJECTED',D),            X
               ('APFL290 DSN:                                          X
                 ',DE),                                                X
               ROUTCDE=(11)
APFL290D EQU   *-2-44,44
         LA    R15,24
         B     MAINRCXX
*--------------------------------------------------------------------*
*              MAIN RETURN EXIT                                      *
*--------------------------------------------------------------------*
MAINRCXX DS    0H
         LR    R2,R15              SAVE RC
         AUTH  OFF                  GET UNAUTHORIZED
*
         TM    WFLAG,$ENQ          ENQ BEING HELD
         BZ    MAINRCX1
         DEQ   (ENQMAJ,ENQMIN,7,SYSTEM)
         NI    WFLAG,255-$ENQ      TURN OFF ENQ
MAINRCX1 DS    0H
*
         LR    R15,R2              RESTORE RC
         L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)              RESTORE REGS
         BR    R14                         EXIT
         SPACE 3
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC PROCESSING                                    *
*                                                                    *
*--------------------------------------------------------------------*
DYNAMIC  DS    0H
         CSVAPF REQUEST=QUERY,DSNAME=APFDSN,                           X
               VOLTYPE=ANY,VOLUME=APFVOL,                              X
               MF=(E,WCSVAPF)
         TM    WFLAG,$REMOVE       IS IT 'REMOVE                040411
         BO    DYNREM00             YES                         040411
         C     R15,=A(4)
         BL    DSNNOK              DSN ALREADY THERE
         BE    DYNADD
         B     DYNERR
DYNREM00 DS    0H                                               040411
         C     R15,=A(4)                                        040411
         BL    DYNREM10            DSN THERE, YES WE CAN REMOVE 040411
         BE    REMDSNER            IF NOT THERE, WE CANNOT      040411
         B     DYNERR                                           040411
*--------------------------------------------------------------------*
*              ADD THE DATASET                                       *
*--------------------------------------------------------------------*
DYNADD   DS    0H
         CSVAPF REQUEST=ADD,DSNAME=APFDSN,                             X
               VOLTYPE=ANY,VOLUME=APFVOL,                              X
               MF=(E,WCSVAPF)
         C     R15,=A(4)
         BE    DSNNOK              DSN ALREADY THERE
         BL    APFADDED
DYNERR   DS    0H
         MVC   APFL310D,APFDSN
         XR    R0,R0               THIS MUST BE HERE
         WTO   ('APFL310 THE FOLLOW IN DATASET NOT ADDED TO ',D),      X
               ('APFL310 APFTABLE DUE TO APF PARAM ERROR',D),          X
               ('APFL310 DSN:                                          X
                 ',DE),                                                X
               ROUTCDE=(11)
APFL310D EQU   *-2-44,44
         LA    R15,24
         B     MAINRCXX
*--------------------------------------------------------------------*
*              REMOVE THE DATASET                               040411
*--------------------------------------------------------------------*
DYNREM10 DS    0H                                               040411
         CSVAPF REQUEST=DELETE,DSNAME=APFDSN,                          X
               VOLTYPE=ANY,VOLUME=APFVOL,                              X
               MF=(E,WCSVAPF)
         C     R15,=A(4)                                        040411
         BL    APFREMOV                                         040411
*
         MVC   APFL320D,APFDSN                                  040411
         XR    R0,R0               THIS MUST BE HERE            040411
         WTO   ('APFL320 THE FOLLOW IN DATASET WAS NOT REMOVED',D),    X
               ('APFL320 FROM THE APFTABLE              ',D),          X
               ('APFL320 DSN:                                          X
                 ',DE),                                                X
               ROUTCDE=(11)                                     040411
APFL320D EQU   *-2-44,44                                        040411
         LA    R15,24                                           040411
         B     MAINRCXX                                         040411
         LTORG ,
         DROP  R12
*--------------------------------------------------------------------*
*                                                                    *
*              OPEN APFLIB AND GET DSNAME AND LENGTH                 *
*                                                                    *
*--------------------------------------------------------------------*
         USING *,R12
OPEN     DS    0H
         STM   R14,R12,12(R13)             SAVE REGS
         LR    R12,R15
         LA    R2,72(R13)
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
         SPACE 2
         LA    R9,APFJFCB
         OPEN  (APFLIB,(INPUT))
         SPACE 2
         RDJFCB (APFLIB)
         LTR   R15,R15
         BNZ   OPENRC04
         SPACE 2
         MVC   APFDSN,0(R9)        MOVE IN DSN
         MVC   APFVOL,118(R9)      MOVE IN VOLSER
         CLOSE (APFLIB)            CLOSE
         SPACE 2
         TRT   APFDSN,TRTTBL       FIND 1ST BLANK
         BZ    OPEN0300
         S     R1,=A(APFDSN)       MINUS @ OF APFDSN
         STC   R1,APFDSNL          SAVE LENGTH
         B     OPENRCXX
OPEN0300 MVI   APFDSNL,X'2C'       SET DSN LEN = 44
         B     OPENRCXX
OPENRC04 CLOSE (APFLIB)                    PARM SET BRCH
         LA    R15,4
OPENRCXX L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)              RESTORE REGS
         BR    R14                         EXIT
TRTTBL   DC    256X'0'
         ORG   TRTTBL+C' '
         DC    X'04'
         ORG   TRTTBL+256
         DROP  R12
         SPACE 3
*--------------------------------------------------------------------*
*                                                                    *
*              GET OLD APFTABLE ADDRESS                              *
*                                                                    *
*--------------------------------------------------------------------*
         USING *,R12
GETAPFAD DS    0H
         STM   R14,R12,12(R13)             SAVE REGS
         LR    R12,R15
         LA    R2,72(R13)
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
         ENQ   (ENQMAJ,ENQMIN,E,7,SYSTEM)
         OI    WFLAG,$ENQ
         L     R11,X'10'                   CVT ADDRESS
         MVC   APFOTBL@(4),CVTAUTHL-CVT(R11)
         CLC   APFOTBL@,=X'7FFFF001' DYNAMIC IN EFFECT
         BE    GAPFDYN                YES
         CLI   APFOTBL@,X'00'
         BE    GAPFRC00
         LA    R15,4
         B     GAPFRCXX
GAPFRC00 DS    0H
         LA    R15,0
GAPFRCXX DS    0H
         L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)              RESTORE REGS
         BR    R14                         EXIT
GAPFDYN  DS    0H
         OI    WFLAG,$DYN
         B     GAPFRC00
         DROP  R12
         SPACE 3
*--------------------------------------------------------------------*
*                                                                    *
*              SCAN APFTABLE TO SEE IF DSN ALREADY THERE             *
*                                                                    *
*--------------------------------------------------------------------*
SEARCH   DS    0H
         USING *,R12
         STM   R14,R12,12(R13)             SAVE REGS
         LR    R12,R15
         LA    R2,72(R13)
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
         XR    R15,R15
         SPACE 3
         L     R9,APFOTBL@
         LA    R8,2(,R9)           POINT TO 1ST ENTRY
         XR    R6,R6
         IC    R6,1(,R9)             255 ENTRY SUPPORT
*        LH    R6,0(,R9)           > 255 ENTRY SUPPORT          *KMB*
         CH    R6,H255             MAX ENTRIES
         BH    SRCHRC08              255 ENTRY SUPPORT
         B     SRCH0200                                         *KMB*
         SPACE 2
*--------------------------------------------------------------------*
*              SCAN THRU APF TABLE                                   *
*--------------------------------------------------------------------*
SRCH0200 XR    R7,R7
         IC    R7,0(,R8)           ENTRY LENGTH
         CH    R7,=H'50'           IS IT VALID LENGTH
         BH    SRCHRC12             NO, ERROR
         SH    R7,H7               -L'DSNL-L'VOLSER
         MVC   SAVEDSN(44),BLANKS
         EX    R7,MVCDSN1
         MVC   SAVEVOL,1(R8)       MOVE IN VOLSER
         CLC   SAVEVOL(50),APFVOL  IS IT THE SAME
         BE    SRCHRC04             YES,
         LA    R8,8(R7,R8)          NO, NEXT ENTRY
         BCT   R6,SRCH0200         LOOP
         ST    R8,APFOTBLE         SAVE CURRENT END OF APFTBLE
         XR    R15,R15
         B     SRCHEXIT
         SPACE 2
SRCHRC12 DS    0H
         LA    R15,12              BAD ENTRIES
         B     SRCHEXIT
SRCHRC08 DS    0H
         LA    R15,8               MAX ENTRIES
         B     SRCHEXIT
SRCHRC04 LA    R15,4               DATASET ALREADY THERE
SRCHEXIT L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)              RESTORE REGS
         BR    R14                         EXIT
MVCDSN1  MVC   SAVEDSN(*-*),7(R8)
         DROP  R12
*--------------------------------------------------------------------*
*                                                                    *
*              GETMAIN NEW APFTABLE                                  *
*                                                                    *
*--------------------------------------------------------------------*
         USING *,R12
GETMAIN  DS    0H
         STM   R14,R12,12(R13)             SAVE REGS
         LR    R12,R15
         LA    R2,72(R13)
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
         L     R15,=A(ADDLEN)
         BALR  R14,R15                     STD LINKAGE
         L     R2,NEWAPFLN
         MODESET KEY=ZERO,MODE=SUP
         GETMAIN RC,LV=(2),SP=245
         ST    R1,NEWAPF@
         STH   R15,GETMRETC
         MODESET KEY=NZERO,MODE=PROB
         XR    R15,R15
         CLC   GETMRETC(2),ZERO
         BNH   GETMRCXX
         LA    R15,4
GETMRCXX L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)              RESTORE REGS
         BR    R14                         EXIT
         DROP  R12
*--------------------------------------------------------------------*
*                                                                    *
*              ADD NEW DSN ENTRY LENTH TO CURRENT TABLE LENGTH       *
*                                                                    *
*--------------------------------------------------------------------*
         USING *,R12
ADDLEN   DS    0H
         STM   R14,R12,12(R13)             SAVE REGS
         LR    R12,R15
         LA    R2,72(R13)
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
         SPACE 2
         L     R2,APFOTBLE         GET END OF TABLE
         S     R2,APFOTBL@         MINUS BEGINNING
         XR    R3,R3
         IC    R3,APFDSNL          GET DSN LENGTH
         LA    R2,7(R2,R3)         ADD LENGTH+VOLSER TO DSNLEN
         ST    R2,NEWAPFLN         NEW ENTRY LENGTH
         SPACE 1
         XR    R15,R15
         L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)              RESTORE REGS
         BR    R14                         EXIT
         DROP  R12
         SPACE 3
*--------------------------------------------------------------------*
*                                                                    *
*              ASK IF OK TO ADD DSNAME TO APFTABLE                   *
*                                                                    *
*--------------------------------------------------------------------*
         USING *,R12
ASKIFOK  EQU   *
         STM   R14,R12,12(R13)             SAVE REGS
         LR    R12,R15
         LA    R2,72(R13)
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
*
*              PUT WTOR HERE TO ASK IF OK
*
         XR    R0,R0
ASKRC00  XR    R15,R15
ASKRCXX  L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)              RESTORE REGS
         BR    R14                         EXIT
         DROP  R12
*--------------------------------------------------------------------*
*                                                                    *
*              BUILD NEW APFTABLE                                    *
*                                                                    *
*--------------------------------------------------------------------*
         USING *,R12
BUILDNEW DS    0H
         STM   R14,R12,12(R13)             SAVE REGS
         LR    R12,R15
         LA    R2,72(R13)
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
         L     R15,=A(MOVEAPF)
         BALR  R14,R15                     STD LINKAGE
         L     R15,=A(ADDDSN)
         BALR  R14,R15                     STD LINKAGE
         L     R15,=A(UPDCVT)
         BALR  R14,R15                     STD LINKAGE
         L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)              RESTORE REGS
         BR    R14                         EXIT
         DROP  R12
         SPACE 3
*--------------------------------------------------------------------*
*                                                                    *
*              MOVE CURRENT APFTABLE TO NEW TABLE                    *
*                                                                    *
*--------------------------------------------------------------------*
         USING *,R12
MOVEAPF  DS    0H
         STM   R14,R12,12(R13)             SAVE REGS
         LR    R12,R15
         LA    R2,72(R13)
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
         SPACE 2
         L     R9,APFOTBL@         OLD TABLE ADDRESS
         L     R2,NEWAPF@          NEW TABLE ADDRESS
         L     R3,NEWAPFLN         NEW TABLE LENGTH
         LR    R4,R9
*        XR    R6,R6               THIS SEEMS
*        IC    R6,0(,R9)             TO DO
*        SLL   R6,3                 NOTHING
         L     R5,APFOTBLE         OLD TABLE END
         SR    R5,R9               MINUS BEGINNING
         MODESET KEY=ZERO,MODE=SUP
         SPACE 1
         MVCL  R2,R4               MOVE CURRENT APF TO NEW
         SPACE 1
         MODESET KEY=NZERO,MODE=PROB
         SPACE 2
         XR    R15,R15
         L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)              RESTORE REGS
         BR    R14                         EXIT
         DROP  R12
*--------------------------------------------------------------------*
*                                                                    *
*              ADD NEW ENTRY TO NEW TABLE                            *
*                                                                    *
*--------------------------------------------------------------------*
         USING *,R12
ADDDSN   DS    0H
         STM   R14,R12,12(R13)             SAVE REGS
         LR    R12,R15
         LA    R2,72(R13)
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
         SPACE 2
         XR    R15,R15
         L     R9,NEWAPF@
         L     R8,APFOTBLE         OLD TABLE END
         S     R8,APFOTBL@         MINUS BEGINNING = OLD LENGTH
         AR    R8,R9               POINT AT END OF NEW
         IC    R6,APFDSNL          ADD IN
         LA    R6,6(,R6)            NEW
         STC   R6,APFDSNL            DSN ENTRY
         MODESET KEY=ZERO,MODE=SUP
         EX    R6,ADDMVC1          MOVE IN NEW DSN TO END OF TBL
         L     R2,NEWAPFLN
         SLR   R2,R2
*        LH    R2,0(,R9)           > 255 ENTRY SUPPORT          *KMB*
         LH    R2,0(,R9)             255 ENTRY SUPPORT          *KMB*
         LA    R2,1(,R2)
*        STH   R2,0(,R9)           > 255 ENTRY SUPPORT          *KMB*
         STC   R2,1(,R9)             255 ENTRY SUPPORT          *KMB*
         MODESET KEY=NZERO,MODE=PROB
         SPACE 2
         XR    R15,R15
         L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)              RESTORE REGS
         BR    R14                         EXIT
ADDMVC1  MVC   0(0,R8),APFDSNL
         DROP  R12
         SPACE 3
*--------------------------------------------------------------------*
*                                                                    *
*              INSERT NEW APFTABLE IN CVT                            *
*                                                                    *
*--------------------------------------------------------------------*
         USING *,R12
UPDCVT   DS    0H
         STM   R14,R12,12(R13)             SAVE REGS
         LR    R12,R15
         LA    R2,72(R13)
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
         SPACE 2
         MODESET KEY=ZERO,MODE=SUP
         SPACE 2                   USE COMPARE AND SWAP
         L     R11,X'10'           CVT ADDRESS
         L     R1,CVTAUTHL-CVT(,R11)
         L     R2,NEWAPF@
UPDC0120 CS    R1,R2,CVTAUTHL-CVT(R11)
         BC    4,UPDC0120          ITS CHANGED
         SPACE 2
         MODESET KEY=NZERO,MODE=PROB
         SPACE 2
         TM    WFLAG,$ENQ          ENQ BEING HELD
         BZ    UPDC0140
         DEQ   (ENQMAJ,ENQMIN,7,SYSTEM)
         NI    WFLAG,255-$ENQ      TURN OFF ENQ
UPDC0140 DS    0H
         SPACE 2
         XR    R15,R15
         L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)              RESTORE REGS
         BR    R14                         EXIT
         DROP  R12
*--------------------------------------------------------------------*
*                                                                    *
*              STIMER                                                *
*                                                                    *
*--------------------------------------------------------------------*
         USING *,R12
STIMER   DS    0H
         STM   R14,R12,12(R13)             SAVE REGS
         LR    R12,R15
         LA    R2,72(R13)
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
         STIMER REAL,DINTVL=INTVL
         XR    R15,R15
         L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)              RESTORE REGS
         BR    R14                         EXIT
         DROP  R12
         SPACE 3
*--------------------------------------------------------------------*
*                                                                    *
*              FREEMAIN OLD APFTABLE                                 *
*                                                                    *
*--------------------------------------------------------------------*
         USING *,R12
FREEMAIN DS    0H
         STM   R14,R12,12(R13)             SAVE REGS
         LR    R12,R15
         LA    R2,72(R13)
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
         XR    R15,R15
         L     R2,APFOTBL@
         L     R3,APFOTBLE
         SR    R3,R2
         MODESET KEY=ZERO,MODE=SUP
         FREEMAIN RC,LV=(3),A=(2),SP=245
         STH   R15,GETMRETC
         MODESET KEY=NZERO,MODE=PROB
         XR    R15,R15
         CLC   GETMRETC(2),ZERO
         BNH   FREERCXX
         LA    R15,4
FREERCXX L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)              RESTORE REGS
         BR    R14                         EXIT
         DC    F'0'
         DROP  R12
DATAAREA EQU   *
INTVL    DC    C'00000500'
ENQMAJ   DC    CL8'KMBAPFQ'
ENQMIN   DC    CL7'APFLIST'
APFOTBL@ DC    F'0'
APFOTBLE DC    F'0'
NEWAPF@  DC    F'0'
NEWAPFLN DC    F'0'
GETMRETC DC    H'0'
BLANKS   DC    CL44' '
WFLAG    DC    AL1(0)
$ENQ     EQU   X'80'               ENQ IS BEING HELD
$DYN     EQU   X'40'               DYNAMIC IN USE
$REMOVE  EQU   X'20'               REMOVE DSN FROM APFTABLE
SAVETBL  DC    0XL50'00'  --+
SAVEVOL  DC    XL6'0'       |
SAVEDSN  DC    XL44'00'   <-+
APFDSNL  DC    X'00'    ---+       KEEP
APFVOL   DC    CL6' '      |        THESE
APFDSN   DC    CL44' '  <--+         TOGETHER
APFEXLST DC    0F'0',X'87',AL3(APFJFCB)
APFJFCB  DC    XL176'00'
APFLIB   DCB   DDNAME=APFLIB,DSORG=PO,EXLST=APFEXLST,MACRF=(R)
         LTORG ,
H4       DC    H'0004'
H8       DC    H'0008'
H7       DC    H'0007'
H255     DC    H'0255'
ZERO     DC    F'0'
SAVEAREA DS    (5*9)D'0'
WCSVAPFX CSVAPF MF=(L,WCSVAPF)
         DROP  R10
         YREGS
*--------------------------------------------------------------------*
*              DSECT                                                 *
*--------------------------------------------------------------------*
         CVT   DSECT=YES,LIST=NO
         END
