*--------------------------------------------------------------------*
*                                                                    *
*  PURPOSE:    COPY PARM='..' TO //PARM   DD   DATASET               *
*                                                                    *
*  ATTR:       RENT REUSE AMODE31 RMODE24                            *
*                                                                    *
*--------------------------------------------------------------------*
GENPARM  AMODE 31
GENPARM  RMODE 24
GENPARM  CSECT
         SAVE  (14,12),T,MCLANE_&SYSDATE._&SYSTIME.
         LR    R12,R15
         USING GENPARM,R12
         LR    R11,R1              SAVE PARM ADDR
         GETMAIN R,LV=WLEN
         XC    0(WLENX,R1),0(R1)   ZERO WORK AREA
*
         ST    R1,8(,R13)           SAVE
         ST    R13,4(,R1)            AREAS
         LR    R13,R1              TOGETHER
         USING WORK,R13
*--------------------------------------------------------------------*
*              INITIALIZE REENTRANT AREAS                            *
*--------------------------------------------------------------------*
         MVC   WOPEN,OPEN          MODEL OPEN
         MVC   WDCB,PARMDCB        MODEL DCB
*
         OPEN  (WDCB,(OUTPUT)),MF=(E,WOPEN)
         TM    WDCB+X'30',X'10'    DID IT OPEN
         BZ    ABEND1               NO
         L     R11,0(,R11)         GET PARM ADDR
         LTR   R11,R11             IS IT ZERO
         BZ    ABEND2               YES, ABEND
         LH    R10,0(,R11)         GET LENGTH
         LTR   R10,R10             IS IT <= 0
         BNP   ABEND2               YES, ABEND
         C     R10,=F'80'          IS PARM > 80
         BNH   PARMOK               NO
         L     R10,=F'80'           YES, SET TO 80
PARMOK   DS    0H
         BCTR  R10,0               -1 FOR EXECUTE
         MVC   WDATA,BLANKS        CLEAR FOR MOVE
         EX    R10,MVCPARM         MOVE IN DATA
         PUT   WDCB,WDATA          PUT IT OUT
         CLOSE (WDCB),MF=(E,WOPEN) CLOSE UP AND GO HOME
         SPACE 1
         LR    R1,R13
         L     R13,4(,R13)         PREV SAVE
         FREEMAIN R,LV=WLEN,A=(1)
         SPACE 1
         RETURN (14,12),RC=0       BYE
         SPACE 1
MVCPARM  MVC   WDATA(0),2(R11)
         SPACE 1
ABEND1   DS    0H
         WTO   'GENPARM1 PARM DDNAME OPEN ERROR',ROUTCDE=11
         ABEND 1
ABEND2   DS    0H
         WTO   'GENPARM2 PARM IS MISSING',ROUTCDE=11
         ABEND 2
*--------------------------------------------------------------------*
*              DATA                                                  *
*--------------------------------------------------------------------*
         LTORG ,
BLANKS   DC    CL256' '
OPEN     OPEN  (0,(OUTPUT)),MF=L
PARMDCB  DCB   DDNAME=PARM,DSORG=PS,DEVD=DA,MACRF=PM,                  X
               RECFM=FB
DCBLEN   EQU   *-PARMDCB
*--------------------------------------------------------------------*
*              DSECTS                                                *
*--------------------------------------------------------------------*
WORK     DSECT
WSAVE    DS    9D
WDCB     DS    CL(DCBLEN),0F
WOPEN    DS    F
WLENX    EQU   *-WORK
WDATA    DS    CL256' '
         DS    0D
WLEN     EQU   *-WORK
         YREGS ,
         END   ,
