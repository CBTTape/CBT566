&GEN     SETC  'GEN'
***********************************************************************
*     THIS VERSION DOES NOT USE AN SVC TO AUTHORIZE.                  *
*     THIS PROGRAM MUST BE LISTED IN THE IKJEFTE2 TABLE (AUTHCMD)     *
***********************************************************************
         SPACE 3
*--------------------------------------------------------------------*
*              REGISTER AND OTHER EQUATES                            *
*--------------------------------------------------------------------*
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 2
         SPACE 3
         PRINT ON
*--------------------------------------------------------------------*
*              $$SUB MACRO                                           *
*--------------------------------------------------------------------*
         PRINT OFF
         MACRO
&L       $$SUB
&L       STM   R14,R12,12(R13)     SAVE REGS
         LA    R14,72(,R13)        NEXT SAVE
         ST    R14,8(,R13)         CHAIN
         ST    R13,4(,R14)          SAVE
         LR    R13,R14             NEW SAVE
         MEND
         PRINT ON
         EJECT
DUMPPSCB TITLE 'DUMPPSCB - '
DUMPPSCB CSECT
DUMPPSCB AMODE 31
DUMPPSCB RMODE 24
         SAVE  (14,12),T,'DUMPPSCB_&SYSDATC._&SYSTIME.'
         LR    R12,R15            POINT BASE REGISTER TO ENTRY POINT
         USING DUMPPSCB,R12
         LA    R2,SAVEAREA        POINT TO SAVEAREA
         ST    R13,SAVEAREA+4     POINTER TO CALLERS SAVEAREA
         ST    R2,8(R13)          POINTER TO CALLED SAVEAREA
         LR    R13,R2             STANDARD POINTER TO SAVEAREA
         SR    R2,R2              CLEAR R2
*--------------------------------------------------------------------*
*     GET PSCB ADDRESS - OTHER CONTROL BLOCKS GOTTEN FROM HERE.      *
*--------------------------------------------------------------------*
*                                  GET TO THE PSCB FROM ASXBITCB
         L     R2,X'224'           POINT TO ASCB
         L     R2,X'6C'(,R2)       POINT TO ASXB
         L     R2,X'F8'(,R2)       POINT TO ITCB OFF THE ASXB
         L     R2,X'B4'(,R2)       POINT TO JSCB.
         L     R2,X'108'(,R2)      POINT TO PSCB.
         ST    R2,PSCBADDR         STORE ITS ADDRESS
         LTR   R2,R2               IS THERE A PSCB (IS THIS A TSU?)
         BZ    NOPSCB              NO PSCB, NOT A TSU
*--------------------------------------------------------------------*
*              AUTHORIZATION WITH AN SVC                             *
*--------------------------------------------------------------------*
*        LA    R1,=C'AUTH'
*        SVC   245
*--------------------------------------------------------------------*
*         AUTHORIZATION WITHOUT AN SVC - AUTHCMD IN PARMLIB          *
*--------------------------------------------------------------------*
* -- >       Test if this command is being run APF Authorized
         STM   R15,R1,SAVE14T1     Now save registers and test if APF
         XR    R15,R15                  authorized.
         TESTAUTH FCTN=1           Are we authorized?
         LTR   R15,R15             Test the return code.
         BNZ   NOTAUTH             Not zero, tell not authorized.
         LM    R15,R1,SAVE14T1     Restore registers used by TESTAUTH.
* -- >                             We're authorized. Go Key 0.
         MODESET MODE=SUP,KEY=ZERO
MODEON   DS    0H
*--------------------------------------------------------------------*
*              DUMP PSCB                                             *
*--------------------------------------------------------------------*
*
         L     R2,PSCBADDR         GET THE SAVED PSCB ADDRESS
         LR    R4,R2               PUT IN PLACE FOR DUMP PGMS
         LA    R0,X'6C'            LENGTH OF PSCB
         CVD   R0,DEC              CONVERT TO DECIMAL, ETC.
         MVC   DEC+08(6),=X'402020202120'
         ED    DEC+08(6),DEC+5
         MVC   MSG1+30(4),DEC+08+2
         ST    R0,DEC
         UNPK  DEC+8(05),DEC+2(3)
         MVC   MSG1+35(3),DEC+8+1
         TR    MSG1+35(3),TRANS-240
         LA    R1,MSG1             PRINT HEADER, DUMP, AND TRAILER
         BAL   R14,PRTDMSG
         LR    R1,R4
         BAL   R14,PRTDUMP
         LA    R1,MSG2
         BAL   R14,PRTDMSG
*--------------------------------------------------------------------*
*              DUMP UPT                                              *
*--------------------------------------------------------------------*
         L     R2,PSCBADDR         GET THE SAVED PSCB ADDRESS
         L     R2,X'34'(,R2)       UPT FROM THE PSCB
         ST    R2,UPTADDR          STORE IT FOR THE RECORD
         LR    R4,R2               PUT IN PLACE FOR DUMP PGMS
         LH    R0,0(,R2)           LENGTH OF UPT
         CVD   R0,DEC              CONVERT TO DECIMAL, ETC.
         MVC   DEC+08(6),=X'402020202120'
         ED    DEC+08(6),DEC+5
         MVC   MSG3+30(4),DEC+08+2
         ST    R0,DEC
         UNPK  DEC+8(05),DEC+2(3)
         MVC   MSG3+35(3),DEC+8+1
         TR    MSG3+35(3),TRANS-240
         LA    R1,MSG3             PRINT HEADER, DUMP, AND TRAILER
         BAL   R14,PRTDMSG
         LR    R1,R4
         BAL   R14,PRTDUMP
         LA    R1,MSG4
         BAL   R14,PRTDMSG
*--------------------------------------------------------------------*
*              DUMP ECT                                              *
*--------------------------------------------------------------------*
         L     R2,PSCBADDR         GET THE SAVED PSCB ADDRESS
         L     R2,X'30'(,R2)       RELOGON BUFFER ADDRESS
         L     R2,X'100'(,R2)      ECT FROM THE RELOGON BUFFER
         ST    R2,ECTADDR          STORE IT FOR THE RECORD
         LR    R4,R2               PUT IN PLACE FOR DUMP PGMS
         LA    R0,X'38'            LENGTH OF ECT
         CVD   R0,DEC              CONVERT TO DECIMAL, ETC.
         MVC   DEC+08(6),=X'402020202120'
         ED    DEC+08(6),DEC+5
         MVC   MSG5+30(4),DEC+08+2
         ST    R0,DEC
         UNPK  DEC+8(05),DEC+2(3)
         MVC   MSG5+35(3),DEC+8+1
         TR    MSG5+35(3),TRANS-240
         LA    R1,MSG5             PRINT HEADER, DUMP, AND TRAILER
         BAL   R14,PRTDMSG
         LR    R1,R4
         BAL   R14,PRTDUMP
         LA    R1,MSG6
         BAL   R14,PRTDMSG
*--------------------------------------------------------------------*
*              AND EXIT                                              *
*--------------------------------------------------------------------*
         MODESET MODE=PROB,KEY=NZERO
*--------------------------------------------------------------------*
ENDPGM   DS    0H
*        SLR   R1,R1                 WE ARE NOT USING THE SVC
*        SVC   245                   DE-AUTH USING AN SVC
         L     R13,4(,R13)
         RETURN (14,12),RC=0
*--------------------------------------------------------------------*
NOTAUTH  DS    0H
         APUT  MSG7,L'MSG7
         B     ENDPGM
*--------------------------------------------------------------------*
NOPSCB   DS    0H
         APUT  MSG8,L'MSG8
         B     ENDPGM
*--------------------------------------------------------------------*
*              WTO MESSAGES, ETC.                                    *
*--------------------------------------------------------------------*
MSG1     WTO   '*** DUMP OF PSCB - LENGTH=XXXX/XXX ***',MF=L,          X
               ROUTCDE=11
MSG2     WTO   '*** END  OF PSCB **',MF=L,ROUTCDE=11
MSG3     WTO   '*** DUMP OF UPT  - LENGTH=XXXX/XXX ***',MF=L,          X
               ROUTCDE=11
MSG4     WTO   '*** END  OF UPT  ***',MF=L,ROUTCDE=11
MSG5     WTO   '*** DUMP OF ECT  - LENGTH=XXXX/XXX ***',MF=L,          X
               ROUTCDE=11
MSG6     WTO   '*** END  OF ECT  ***',MF=L,ROUTCDE=11
*--------------------------------------------------------------------*
*              DATA AREAS                                            *
*--------------------------------------------------------------------*
SAVEAREA DC    (4*9)D'0'
SAVE14T1 DS    2D
DEC      DS    2D
PSCBADDR DS    F
ECTADDR  DS    F
UPTADDR  DS    F
MSG7     DC    C'DUMPTSB Program is not Authorized'
MSG8     DC    C'There is no PSCB--Not TSO'
TRANS    DC    C'0123456789ABCDEF'
         LTORG
         DS    0D
         COPY  PRTDUMP
         PRINT GEN
         MVSAREAS PSA=YES,                                             X
               LISTGEN=YES
         END
         COPY  EPUTL
