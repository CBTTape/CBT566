***********************************************************************
* ENVIRONMENT - OS/VS                                                 *
* PURPOSE     -  RETURN TO THE CALLER THE CATALOG IN WHICH THE        *
*                HIGH LEVEL QUALIFER EXIST IN.                        *
*                                                                     *
*        INPUT :  R1 POINTS TO  A(HILVLQUAL)  CL8       INPUT         *
*                               A( CATNAME )  CL44      OUTPUT        *
*                               A( ERRMSG  )  CL80      OUTPUT        *
*                                                                     *
*        OUTPUT: R15 = 0  VALID HIGH LEVEL QUALIFER                   *
*                         CATNAME RETURNED IN CALLERS AREA @ +4       *
*                    > 0  HIGH LEVEL QUALIFER NOT FOUND               *
*                         ERRMSG RETURNED IN CALLERS AREA @ +8        *
*                                                                     *
* ATTR: RENT, REUS                                                    *
*                                                                     *
***********************************************************************
DSCATNAM AMODE 31
DSCATNAM RMODE 24
DSCATNAM CSECT
         SAVE  (14,12),T,*         SAVE REGS
         LR    R12,R15
         USING DSCATNAM,R12
         LR    R11,R1              GET PARAMETERS
         STORAGE OBTAIN,LENGTH=WORKLEN,SP=1
         ST    R1,8(,R13)          CHAIN TO PREVIOUS
         ST    R13,4(,R1)          CHAIN PREV TO OURS
         LR    R13,R1              SET UP OUR SAVE
         USING WORK,R13
         SPACE 3
         USING CATPARM,R11
*--------------------------------------------------------------------*
*              GET CATALOG NAME FROM ALIAS ENTRY                     *
*--------------------------------------------------------------------*
         XC    WORKAREA,WORKAREA
         MVC   WCAMLST(CAMLSTLN),CAMLST
         L     R1,CATNAME          @ OF OUTPUT CATNAME
         L     R2,CATALIAS         @ OF INPUT ALIAS
         MVC   0(44,R1),BLANKS     BLANK CATNAME
         MVC   0(8,R1),0(R2)       MOVE IN ALIAS
*
         L     R1,CATNAME          @ OF ALIAS / AFTER=CATNAME
         ST    R1,WCAMLST+4
         LA    R1,WORKAREA         @ OF LOCATE OUTPUT AREA
         ST    R1,WCAMLST+12
         LA    R1,WCAMLST          @ OF CAMLST
         LOCATE (1)                SET CATNAME FROM ALIAS
         LTR   R15,R15             OK?
         BNZ   ERRCAT               NO, ERROR
         SPACE 3
RETURN   DS    0H
         LR    R1,R13
         LR    R2,R15
         L     R13,4(,R13)            RESTORE LINK REG
         STORAGE RELEASE,LENGTH=WORKLEN,ADDR=(1),SP=1
         LR    R15,R2
         RETURN (14,12),T,RC=(15)     RETURN TO CALLER
*--------------------------------------------------------------------*
*              ERRORS                                                *
*--------------------------------------------------------------------*
ERRCAT   DS    0H
         L     R1,CATMSG           @ OF MESSAGE AREA
         L     R2,CATALIAS         @ OF ALIAS NAME
         MVC   0(80,R1),BLANKS     BLANKS MESSAGE AREA
         MVC   0(L'CATERR01,R1),CATERR01 MOVE IN OUR MESSAGE
         MVC   4(8,R1),0(R2)       MOVE IN ALIAS NAME
         LA    R15,8               SET RETURN CODE
         B     RETURN
         SPACE 3
*--------------------------------------------------------------------*
*              DATA AREAS                                            *
*--------------------------------------------------------------------*
CAMLST   CAMLST NAME,2,,4
CAMLSTLN EQU   *-CAMLST
CATERR01 DC    C'*** XXXXXXXX UNKNOWN ALIAS ***'
BLANKS   DC    CL80' '
         LTORG ,
*--------------------------------------------------------------------*
*              DSECTS                                                *
*--------------------------------------------------------------------*
WORK     DSECT
WSAVE    DC    9D'0'
WCAMLST  CAMLST NAME,2,,4
WORKAREA DC    XL256'00'           WORK AREA FOR LOCATE
WDEC     DC    2D'0'
WORKLEN  EQU   *-WORK
         SPACE 3
*--------------------------------------------------------------------*
*              PARAMETER DSECT                                       *
*--------------------------------------------------------------------*
CATPARM  DSECT ,
CATALIAS DS    A
CATNAME  DS    A
CATMSG   DS    A
         SPACE 3
*--------------------------------------------------------------------*
*              REGISTER EQUATES                                      *
*--------------------------------------------------------------------*
*
R0       EQU   0
R1       EQU   1                     PARAMETER LIST
R2       EQU   2                     BASE REGISTER 1
R3       EQU   3                     BASE REGISTER 2
R4       EQU   4
R5       EQU   5
R6       EQU   6                     DECIMAL CONVERSION WORK REGISTER
R7       EQU   7
R8       EQU   8                     MAXIMUM NUMBER OF LINES PER PAGE
R9       EQU   9                     NUMBER OF LINES PRINTED
R10      EQU   10                    LINE COUNT INCREMENTOR
R11      EQU   11
R12      EQU   12                    BRANCH AND LINK REGISTER
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   ,
