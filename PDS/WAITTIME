*---------------------------------------------------------------
*              THIS PROGRAM WILL WAIT A SPECIFIC NUMBER OF
*              SECONDS PASSED IN THE PARM FIELD
*              FOR A MAX OF 999 SECONDS
*---------------------------------------------------------------
*        ATTRIBUTES: REENTRANT, REUSABLE
*                    AMODE=31, RMODE=24
*---------------------------------------------------------------
*              FROM COBOL
*              WAIT-LEN IS TWO BYTE BINARY (INTEGER)
*              WAIT-SEC IS 3 BYTE CHAR NUMBER.
*        1 WAIT-PARM
*          5 WAIT-LEN PIC 9(4) VALUE 3
*          5 WAIT-SEC PIC X(3) VALUE '010'.
*
*        CALL WAITTIME USING WAIT-PARM.
*---------------------------------------------------------------
WAITTIME RMODE 24
WAITTIME AMODE 31
WAITTIME CSECT ,
         SAVE  (14,12),T,*
         LR    R12,R15
         USING WAITTIME,R12
         LR    R11,R1
         STORAGE OBTAIN,LENGTH=LWORK,SP=1
         ST    R1,8(,R13)          OURS INTO CALLERS
         ST    R13,4(,R1)          CALLERS INTO OURS
         LR    R13,R1              COPY NEW R13
         USING WORK,R13
*--------------------------------------------------------------------*
*              GET PARM ###                                          *
*--------------------------------------------------------------------*
         L     R2,0(,R11)          GET PARAM ADDR
         LH    R3,0(R2)
         LTR   R3,R3               IS ANYTHING THERE
         BNP   EXIT
         C     R3,=F'3'            > THREE CHARACTERS
         BH    EXIT12               YES, ERROR
         BCTR  R3,0                -1
         EX    R3,MOVE             MOVE INTO WDEC
         SPACE 1
PACK0000 EX    R3,PACK
         SPACE 1
         CVB   R1,WTIME            CONVERT TO BINARY
         MH    R1,=H'100'          CONVERT TO HUNDRETH OF SEC.
         LA    R1,1(,R1)           +1 IN CASE ITS ZERO
         ST    R1,WINTVL           SAVE
         STIMER WAIT,BINTVL=WINTVL WAIT
EXIT     DS    0H
         LA    R15,0
         B     EXITXX
EXIT12   DS    0H
         WTO   'WAITTIME PARM FIELD LARGER THAN 3 DIGITS',      XXXXXXXX
               ROUTCDE=11
         LA    R15,12
         B     EXITXX
EXITXX   DS    0H
         LR    R5,R15
         LR    R1,R13
         L     R13,4(,R13)
         STORAGE RELEASE,LENGTH=LWORK,ADDR=(1),SP=1
         LR    R15,R5
         RETURN (14,12),RC=(15)
         ORG   *-2                 BACKUP OVER BR  14
         BSM   0,R14
*--------------------------------------------------------------------*
*        STATIC DATA AREAS                                           *
*--------------------------------------------------------------------*
MOVE     MVC   WDEC(0),2(R2)
PACK     PACK  WTIME(8),WDEC(0)
         LTORG ,
*--------------------------------------------------------------------*
*        DSECTS                                                      *
*--------------------------------------------------------------------*
WORK     DSECT ,
WSAVE    DS    9D
WDEC     DC    2D'0'
WTIME    DC    2D'0'
WINTVL   DC    F'0'
LWORK    EQU   *-WORK
         YREGS
         END   ,
