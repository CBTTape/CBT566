&GEN     SETC  'GEN'
*--------------------------------------------------------------------*
*              PROGRAM TO DUMP TIOT                                  *
*--------------------------------------------------------------------*
         SPACE 3
*--------------------------------------------------------------------*
*              $$SUB MACRO                                           *
*--------------------------------------------------------------------*
         PRINT OFF
         MACRO
&L       $$SUB
&L       STM   R14,R12,12(R13)     SAVE REGS
         LA    R14,72(,R13)        NEXT SAVE
         ST    R14,8(,R13)         CHAIN
         ST    R13,4(,R14)          SAVE
         LR    R13,R14             NEW SAVE
         MEND
         PRINT ON
         EJECT
DUMPTIOT TITLE 'DUMPTIOT - PRODUCE A DUMP OF THE TIOT'
DUMPTIOT CSECT
         SAVE  (14,12),T,'DUMPTIOT_&SYSDATC._&SYSTIME.'
         LR    R12,R15            POINT BASE REGISTER TO ENTRY POINT
         USING DUMPTIOT,R12
         LA    R2,SAVEAREA        POINT TO SAVEAREA
         ST    R13,SAVEAREA+4     POINTER TO CALLERS SAVEAREA
         ST    R2,8(R13)          POINTER TO CALLED SAVEAREA
         LR    R13,R2             STANDARD POINTER TO SAVEAREA
         LR    R2,R3              SAVE ANY PARM POINTER
         EXTRACT TIOTADRS,FIELDS=(TIOT) GET TIOT ADDRESS
         L     R4,TIOTADRS         PICK UP TIOT ADDRESS
         USING TIOT,R4             ADDRESS TIOT
         MVC   MSG1+21(8),0(R4)
         MVC   MSG1+30(8),8(R4)
         MVC   MSG1+39(8),16(R4)
         TPUT  MSG1,L'MSG1
         LA    R3,TIOENTRY
         USING TIOENTRY,R3         ADDRESS TIOT
         B     LOOPVOL
         DROP  R4
LOOPVOL  DS    0H
         CLI   TIOELNGH,0          END OF TIOT?
         BE    CKMVS               GO CHECK FOR MVS SYSTEM
         XR    R0,R0               CLEAR INDEX REG
         IC    R0,TIOELNGH         PICK UP ENTRY LENGTH
         LA    R1,TIOELNGH
         BAL   R14,PRTDUMP
         XR    R1,R1               CLEAR INDEX REG
         IC    R1,TIOELNGH         PICK UP ENTRY LENGTH
         LA    R3,0(R1,R3)         BUMP TO NEXT TIOT ENTRY
         B     LOOPVOL             CONTINUE SEARCH
CKMVS    DS    0H
         TPUT  =CL40'*** END OF TIOT ***',40
         L     R13,4(,R13)
         RETURN (14,12),RC=0
*--------------------------------------------------------------------*
*              MESSAGES                                              *
*--------------------------------------------------------------------*
MSG1     DC    CL70'*** DUMP OF TIOT FOR XXXXXXXX XXXXXXXX XXXXXXXX ***X
               '
*--------------------------------------------------------------------*
*              DATA AREAS                                            *
*--------------------------------------------------------------------*
SAVEAREA DC    (4*9)D'0'
TIOTADRS DC    A(0)
         LTORG ,
         SPACE 2
*--------------------------------------------------------------------*
*              COPY IN PRTDUMP                                       *
*--------------------------------------------------------------------*
         COPY  PRTDUMP
         PRINT GEN
*--------------------------------------------------------------------*
*              DSECTS                                                *
*--------------------------------------------------------------------*
TIOT     DSECT ,
         IEFTIOT1
         YREGS  ,
         END
